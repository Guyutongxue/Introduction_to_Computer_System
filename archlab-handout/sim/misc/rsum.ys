# PKU ICS ArchLab Handin (Part A)
#
# @file rsum.ys
# @author Guyutongxue <1900012983@pku.edu.cn>
# 
# This file is distributed under WTFPL <https://www.wtfpl.net> WITHOUT ANY WARRANTY.

    .pos 0
    irmovq stack, %rsp
    irmovq ele1, %rdi
    call rsum_list
    halt

# sample linked list
    .align 8
ele1:
    .quad 0x00a
    .quad ele2
ele2:
    .quad 0x0b0
    .quad ele3
ele3:
    .quad 0xc00
    .quad 0x0

rsum_list:
    irmovq $0, %r8
    subq %r8, %rdi
    je recursion_end
    pushq %rbx
    mrmovq 0x0(%rdi), %rbx
    mrmovq 0x8(%rdi), %rdi
    call rsum_list
    addq %rbx, %rax
    popq %rbx
    ret
recursion_end:
    irmovq $0, %rax
    ret

    .pos 0x200
stack:
